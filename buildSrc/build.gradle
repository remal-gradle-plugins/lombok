/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

buildscript {
    dependencies {
        classpath 'name.remal.gradle-plugins.toolkit:build-logic:0.69.6'
    }
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

apply plugin: 'name.remal.toolkit.build-logic'

Configuration localGroovyConf = configurations.create('localGroovy') { Configuration conf ->
    conf.dependencies.add(
        project.dependencies.create('name.remal.gradle-api:local-groovy')
    )
}

configurations.create('javaParser') { Configuration conf ->
    configurations.api.extendsFrom(conf)
    conf.defaultDependencies { DependencySet deps ->
        String groupNameNotation = 'com.github.javaparser:javaparser-core'
        String version = localGroovyConf.resolvedConfiguration.lenientConfiguration
            .allModuleDependencies
            .find { "${it.moduleGroup}:${it.moduleName}" == groupNameNotation }
            ?.moduleVersion
            ?: '3.17.0'
        deps.add(
            project.dependencies.create("${groupNameNotation}:${version}")
        )
    }
}
